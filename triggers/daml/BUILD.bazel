# Copyright (c) 2020 The DAML Authors. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

# TODO Once daml_compile uses build instead of package we should use
# daml_compile instead of a genrule.

load("@build_environment//:configuration.bzl", "sdk_version")

DAML_LF_VERSIONS = [
    "1.7",
    "1.8",
    "1.dev",
]

[
    genrule(
        name = "daml-trigger-{}".format(lf_version),
        srcs = glob(["**/*.daml"]),
        outs = ["daml-trigger-{}.dar".format(lf_version)],
        cmd = """
          set -eou pipefail
          TMP_DIR=$$(mktemp -d)
          mkdir -p $$TMP_DIR/daml/Daml/Trigger
          cp -L $(location Daml/Trigger.daml) $$TMP_DIR/daml/Daml
          cp -L $(location Daml/Trigger/Assert.daml) $$TMP_DIR/daml/Daml/Trigger
          cp -L $(location Daml/Trigger/Internal.daml) $$TMP_DIR/daml/Daml/Trigger
          cp -L $(location Daml/Trigger/LowLevel.daml) $$TMP_DIR/daml/Daml/Trigger
          cat << EOF > $$TMP_DIR/daml.yaml
sdk-version: {sdk}
name: daml-trigger
source: daml
version: 0.0.1
exposed-modules:
  - Daml.Trigger
  - Daml.Trigger.Assert
  - Daml.Trigger.LowLevel
dependencies:
  - daml-stdlib
  - daml-prim
build-options: ["--ghc-option", "-Werror", "--target", "{lf_version}"]
EOF
          $(location //compiler/damlc) build --project-root=$$TMP_DIR -o $$PWD/$(location daml-trigger-{lf_version}.dar)
          rm -rf $$TMP_DIR
        """.format(
            lf_version = lf_version,
            sdk = sdk_version,
        ),
        tools = ["//compiler/damlc"],
        visibility = ["//visibility:public"],
    )
    for lf_version in DAML_LF_VERSIONS
]

filegroup(
    name = "daml-trigger",
    srcs = [
        "daml-trigger-{}.dar".format(lf_version)
        for lf_version in DAML_LF_VERSIONS
    ],
    visibility = ["//visibility:public"],
)

genrule(
    name = "daml-trigger-rst-docs",
    srcs = glob(["**/*.daml"]) + [":daml-trigger-rst-template.rst"],
    outs = ["daml-trigger.rst"],
    cmd = """
        $(location //compiler/damlc) -- docs \
            --combine \
            --output=$(location :daml-trigger.rst) \
            --format=Rst \
            --template=$(location :daml-trigger-rst-template.rst) \
            $(location Daml/Trigger.daml) \
            $(location Daml/Trigger/Assert.daml) \
            $(location Daml/Trigger/LowLevel.daml)
    """,
    tools = ["//compiler/damlc"],
    visibility = ["//visibility:public"],
)
